plugins {
    id 'application'
    id 'airbyte-bulk-connector'
    id "io.airbyte.gradle.docker"
}

airbyteBulkConnector {
    core = 'load'
    toolkits = ['load-s3', 'load-avro', 'load-aws']
    cdk = 'local'
}

application {
    mainClass = 'io.airbyte.integrations.destination.s3_v2.S3V2Destination'

    applicationDefaultJvmArgs = [
            '-XX:+ExitOnOutOfMemoryError', '-XX:MaxRAMPercentage=75.0',
//            Uncomment to run locally:
//          '--add-opens', 'java.base/java.lang=ALL-UNNAMED'
//            Uncomment to enable remote profiling:
//            '-XX:NativeMemoryTracking=detail',
//            '-Djava.rmi.server.hostname=localhost',
//            '-Dcom.sun.management.jmxremote=true',
//            '-Dcom.sun.management.jmxremote.port=6000',
//            '-Dcom.sun.management.jmxremote.rmi.port=6000',
//            '-Dcom.sun.management.jmxremote.local.only=false',
//            '-Dcom.sun.management.jmxremote.authenticate=false',
//            '-Dcom.sun.management.jmxremote.ssl=false'
    ]
}

// Uncomment to run locally
//run {
//    standardInput = System.in
//}

// Exclude conflicting log4j-over-slf4j dependency
configurations.all {
        exclude group: "org.slf4j", module: "slf4j-reload4j"
}


airbyte {
    docker {
        imageName.set("destination-s3")
    }
}

//project.ext["docker.buildArgs.ARG1"] = "value1"
import io.airbyte.gradle.GenerateConnectorBuildArgs

def generateConnectorDockerBuildArgs = tasks.register('generateConnectorDockerBuildArgs', GenerateConnectorBuildArgs) {
    metadata.set(layout.projectDirectory.file('metadata.yaml'))
    output.set(layout.buildDirectory.file('docker/buildArgs.properties'))
}

tasks.named('dockerBuildx').configure { task ->

    dependsOn generateConnectorDockerBuildArgs
    inputs.file generateConnectorDockerBuildArgs.flatMap { it.output }

    /*  A Provider<String> that lazily reads the properties file at execution */
    def propsText = providers.fileContents(generateConnectorDockerBuildArgs.flatMap { it.output })
            .asText                                    // Provider<String>
            .orElse('')                                 // handle missing file

    /*  Transform that String -> Map<String,String> lazily as well            */
    def propsMap = propsText.map { txt ->
        txt.readLines()
                .findAll { it.contains('=') }
                .collectEntries { line ->
                    def (k, v) = line.split('=', 2)
                    [(k.trim()): v.trim()]
                }
    }                                                       // Provider<Map<String,String>>

    /*  Inject the map *during configuration*, but its contents are resolved
        only when Gradle needs buildArgs (after the task graph is ready).      */
    task.buildArgs.putAll(propsMap)
}
